#pragma kernel IterateLifeCells

RWStructuredBuffer<int> _Cells0;
RWStructuredBuffer<int> _Cells1;

RWStructuredBuffer<int> _LifeRules;

bool _BufferFlag = false;

// uint _Scale;
uint _ScaleX;
uint _ScaleY;

int GetCellAt(uint2 xy)
{
    if (xy.x < _ScaleX && xy.y < _ScaleY)
    {
        if (_BufferFlag)
            return _Cells1[xy.x + xy.y * _ScaleX];
        else
            return _Cells0[xy.x + xy.y * _ScaleX];
    }
    else
    {
        return false;
    }
}

void SetCellAt(uint2 xy, int value)
{
    if (xy.x < _ScaleX && xy.y < _ScaleY)
    {
        if (_BufferFlag)
            _Cells0[xy.x + xy.y * _ScaleX] = value;
        else
            _Cells1[xy.x + xy.y * _ScaleX] = value;
    }
}

uint CountNeighboursAt(uint2 xy)
{
    return GetCellAt(xy + int2(-1, -1)) +
           GetCellAt(xy + int2(-1,  0)) +
           GetCellAt(xy + int2(-1,  1)) +
           GetCellAt(xy + int2( 0, -1)) +
           GetCellAt(xy + int2( 0,  1)) +
           GetCellAt(xy + int2( 1, -1)) +
           GetCellAt(xy + int2( 1,  0)) +
           GetCellAt(xy + int2( 1,  1));
}

[numthreads(8, 8, 1)]
void IterateLifeCells(uint3 id: SV_DispatchThreadID)
{
    SetCellAt(id.xy, _LifeRules[GetCellAt(id.xy) * 9 + CountNeighboursAt(id.xy)]);
}
